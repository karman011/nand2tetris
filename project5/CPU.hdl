// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
        And(a = instruction[15], b = true, out = IsCIns);
        // if IsCIns == 1 input is C instruction

        Not(in = instruction[15], out = IsAIns);
        // if IsAIns == 1 input is A instruct

        Or(a = IsAIns, b = instruction[5], out = intoA);
        //if this is a A instruction or we need write dest A, intoA = true;
        Mux16(a = instruction, b = outALU, sel = IsCIns, out = inputA);
        // if input is C instruction, the out of ALU feedback to A register.
        ARegister(in = inputA, load = intoA, out = outA, out[0..14] = addressM);
        // if into a = true, we load A regiter.
        And(a = IsCIns, b = instruction[12], out = AorM);
        Mux16(a = outA, b = inM, sel = AorM, out = AM);
        // A or M input into ALU decide by istruction[12];
        ALU(x = outD, y = AM, zx = instruction[11], nx = instruction[10], zy = instruction[9], ny = instruction[8], f = instruction[7], no = instruction[6], out = outALU, out = outM, zr = zr, ng = ng);
        // just input into alu
        DRegister(in = outALU, load = intoD, out = outD);
        And(a = IsCIns, b = instruction[4], out = intoD);
        // if is C instruction and instruction[4] = true;
        And(a = IsCIns, b = instruction[3], out = writeM);
        // whether write m?

        Not(in = ng, out = pos);
        Not(in = zr, out = nzr);
        And(a = IsCIns, b = instruction[0], out = jgt);
        And(a = pos, b = nzr, out = posANDnzr);

        And(a = jgt, b = posANDnzr, out = j1);
        // j1 = true, when jgt and ALU res is positive and not zero 
        And(a = IsCIns, b = instruction[1], out = jeq);
        And(a = zr, b = jeq, out = j2);
        // j2 = true when jeq and ALU res is zero
        And(a = instruction[15], b = instruction[2], out = jlt);
        And(a = jlt, b = ng, out = j3);
        // j3 = true when jls and ALU res is negative
        Or(a = j1, b = j2, out = jp);
        Or(a = j3, b = jp, out = finaljp);
        // when any of j1, j2, j3 = true, do jump.
        PC(in = outA, load = finaljp, inc = true, reset = reset, out[0..14] = pc);

	//// Replace this comment with your code.
}